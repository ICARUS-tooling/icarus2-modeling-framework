
plugins {
	//id 'com.github.jk1.dependency-license-report' version '1.2'
    id "com.github.hierynomus.license" version "0.14.0"
}

allprojects {
  	apply plugin: 'java'
	apply plugin: 'eclipse'
	
	apply plugin: 'com.github.hierynomus.license'
	
  	sourceCompatibility = 1.8
  	targetCompatibility = 1.8
	
  	group = 'icarus2'
  	version = '0.0.1'

  	repositories {
    	mavenCentral()
  	}  
}

description = 'ICARUS 2 framework root'
	
// Gradle Wrapper
wrapper { 
	gradleVersion = "4.8" 
}

subprojects {
  	
  	tasks.withType(JavaCompile) {
  		options.encoding = 'UTF-8'
    	options.fork = true
 	}
  	
	dependencies {
	    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
		implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

	    // https://mvnrepository.com/artifact/org.slf4j/slf4j-jdk14
		implementation group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.25'

	    // https://mvnrepository.com/artifact/com.google.guava/guava
		implementation group: 'com.google.guava', name: 'guava', version: '25.1-jre'

	    // https://mvnrepository.com/artifact/it.unimi.dsi/fastutil
		implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.2.1'

	    // https://mvnrepository.com/artifact/com.squareup/javapoet
		implementation group: 'com.squareup', name: 'javapoet', version: '1.11.1'
	    
	    // https://mvnrepository.com/artifact/xerces/xercesImpl
		implementation group: 'xerces', name: 'xercesImpl', version: '2.12.0'
	    
        // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
		testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.0-M1'
		// https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
		testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.0-M1'		
		// https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-params
		testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.0-M1'
		

	    // https://mvnrepository.com/artifact/org.mockito/mockito-core
		testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.+'

	}
		
	test {
	    useJUnitPlatform()
	}
	
	downloadLicenses {
	    ext.apacheTwo = license('Apache License, Version 2.0', 'http://opensource.org/licenses/Apache-2.0')
	    ext.bsd = license('BSD License', 'http://www.opensource.org/licenses/bsd-license.php')
	
	    includeProjectDependencies = true

	
	    aliases = [
	        (apacheTwo) : ['The Apache Software License, Version 2.0', 'Apache 2', 'Apache License Version 2.0', 'Apache License, Version 2.0', 'Apache License 2.0', license('Apache License', 'http://www.apache.org/licenses/LICENSE-2.0')],
	        (bsd) : ['BSD', license('New BSD License', 'http://www.opensource.org/licenses/bsd-license.php')]
	    ]
	
	    dependencyConfiguration = 'runtime'
	}
	
	license {
		ext.year = '2014-'+Calendar.getInstance().get(Calendar.YEAR)
		ext.name = 'Markus GÃ¤rtner'
		ext.email = 'markus.gaertner@uni-stuttgart.de'
		
		header = project.file('LICENSE_HEADER')
		includes(["**/*.java", "**/*.properties", "**/*.ini", "**/*.xml"])
		dryRun = true
	}
}
